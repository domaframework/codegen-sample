import org.h2.Driver
import org.seasar.doma.gradle.codegen.extension.CodeGenConfig
import org.seasar.doma.gradle.codegen.jdbc.SimpleDataSource

buildscript {
    ext.domaVersion = '2.28.0'
    ext.h2Version= '1.4.200'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.org.seasar.doma:codegen:0.0.2'
        classpath 'gradle.plugin.org.seasar.doma:compile:1.0.0'
        classpath "com.h2database:h2:$h2Version"
    }
}

apply plugin: 'java'
apply plugin: 'org.seasar.doma.compile'
apply plugin: 'org.seasar.doma.codegen'

group 'org.seasar.doma'
version '1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.seasar.doma:doma:$domaVersion"
    annotationProcessor "org.seasar.doma:doma:$domaVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
    testRuntimeOnly "com.h2database:h2:$h2Version"
}

def _url = "jdbc:h2:file:$projectDir/data/db"
def _user = ''
def _password = ''

domaCodeGen {
    dev {
        def basePackage = 'sample'
        url = _url
        user = _user
        password = _password
        entity {
            packageName = "${basePackage}.entity"
        }
        dao {
            packageName = "${basePackage}.dao"
        }
    }
}

task createDb {
    doLast {
        def ds = new SimpleDataSource()
        ds.setDriver(new Driver())
        ds.setUrl(_url)
        ds.setUser(_user)
        ds.setPassword(_password)

        def sql = """
            drop all objects;

            create table department(
                department_id integer not null primary key, 
                department_no integer not null,
                department_name varchar(20),
                location varchar(20) default 'tokyo', 
                version integer
             );

            create table employee(
                employee_id integer not null primary key, 
                employee_no integer not null,
                employee_name varchar(20)
            );
            """
        ds.connection.withCloseable {
            it.createStatement().withCloseable {
                it.execute(sql)
            }
        }
    }
}
